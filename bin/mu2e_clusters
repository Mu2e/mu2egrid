#!/usr/bin/perl -w
#
# A simple wrapper for jobsub_q to figure out what job clusters are still in the queue.
#
# A.Gaponenko, 2012, 2015
#
use strict;
use Getopt::Long;
use File::Basename;


use Class::Struct Mu2eClusterStats => { total=>'$', running=>'$', held=>'$', idle=>'$' };

sub Mu2eClusterStats::add {
    my ($self, $other) = @_;

    my %union;
    foreach my $i (keys %$self, keys %$other) {
        $union{$i}++
    }
    my @union = keys %union;

    foreach my $k (@union) {
        my $sum = ($self->{$k} // 0) + ($other->{$k}//0);
        $self->{$k} = $sum;
    }
}

#================================================================
sub jobinfo($) {
    my ($fields) = @_;
    my $code = $$fields[5];
    my %codeToName = ('H' => 'held', 'R' => 'running', 'I'=>'idle');
    my $state = $codeToName{$code} // 'unknown';
    my %keys = ('total'=>1, $state=>1);
    my $res = Mu2eClusterStats->new(%keys);
}
#================================================================
sub getUserClusters($$) {
    my ($username, $groupname) = @_;

    my @args;
    if($username ne '') {
        push @args, '--user', $username;
    }
    if($groupname ne '') {
        push @args, '--group', $groupname;
    }

    open OLDERR,     ">&", \*STDERR or die "Can't dup STDERR: $!\n";
    open STDERR, '>', '/dev/null' or die "Can't redirect STDERR: $!\n";
    my $ret = open(my $CQ, '-|', 'jobsub_q', @args);
    my $savedmsg = $!;
    open STDERR,     ">&", \*OLDERR or die "Can't restor STDERR: $!\n";
    die "Error opening jobsub_q pipe: $savedmsg\n" unless $ret;

    my %clusters;

  LINE:
    while(my $line = <$CQ>) {
        next LINE unless $line =~ /^\d+\.\d+@/;

        chomp($line);
        my @fields = split(' ', $line);

        my $jobuser = $fields[1];
        if(($username eq '')||($jobuser eq $username)) {

            my $cn = $fields[0];
            $cn =~ s/\.\d*@/@/;

            my $current = jobinfo(\@fields);

            if(!defined($clusters{$jobuser})) {
                $clusters{$jobuser} = {};
            }
            if(!defined($clusters{$jobuser}->{$cn})) {
                $clusters{$jobuser}->{$cn} = Mu2eClusterStats->new(total=>0, running=>0, held=>0, idle=>0);
            }

            $clusters{$jobuser}->{$cn}->add($current);
        }
    }

    close $CQ
        or die "Error when running jobsub_q.  Exit code ", $?>>8, ", signal ", 0xFF&$? ,".\n";

    return \%clusters;
}

#================================================================
sub summarizeByUser($) {
    my $uclusters = shift;

    my $res = {};

    my @users = sort(keys %$uclusters);
    foreach my $u (@users) {
        $res->{$u} = Mu2eClusterStats->new(total=>0, running=>0, held=>0, idle=>0);

        my $clusters = $$uclusters{$u};
        foreach my $cn (sort(keys %$clusters)) {
            $res->{$u}->add($$clusters{$cn});
        }
    }

    return $res;
}

#================================================================
sub usage() {
    my $self = basename($0);
    return <<EOF
Usage:

        $self [options]

will summarize information obtained from jobsub_q and will display a
list of all active condor clusters and job counts in the clusters.

Options:

    --user=<username>     Passed to jobsub_q to restict the output to jobs
                          belonging to the given user.  The default is
                          the user who runs mu2e_clusters.
                          See also --allusers.

    --group=<groupname>   Passed to jobsub_q.

    --brief               Just cluster IDs are printed.

    --allusers            Print out information about all users.  This
                          overrides the --user options.

    --summary             Instead of one line per cluster print one line per
                          user with job counts.

EOF
;
}

#================================================================
my $user = `whoami`;
chomp $user;
my $group = '';
my $allusers  = 0;
my $brief = 0;
my $summary = 0;
my %opt = (help => 0, brief => \$brief, user => \$user, group => \$group, allusers => \$allusers, summary=>\$summary);
GetOptions(\%opt, "help", "brief", "user=s", "group=s", "allusers", "summary")
    or die "\nError processing command line options.\n";

if($opt{'help'} or ($#ARGV >= 0)) {
    print usage();
}
else {
    die "Conflicting options --brief and --summary\n" if($brief and $summary);

    if($allusers) {
        $user = '';
    }
    my $uclusters = getUserClusters($user, $group);

    if($summary) {
        my $s = summarizeByUser($uclusters);
        print "njobs\trunning\tidle\theld\tuser\n";

        my @users = sort(keys %$s);
        foreach my $u (@users) {
            print
                $$s{$u}->total,
                "\t", $$s{$u}->running,
                "\t", $$s{$u}->idle,
                "\t", $$s{$u}->held,
                "\t", $u,
                "\n";
        }
    }
    else {

        my $headers = $allusers ?
            "Cluster  \t\t\t\tnjobs\trunning\tidle\theld\tuser\n" :
            "Cluster  \t\t\t\tnjobs\trunning\tidle\theld\n";

        print $headers unless $brief;

        my @users = sort(keys %$uclusters);
        foreach my $u (@users) {
            my $clusters = $$uclusters{$u};

            foreach my $cn (sort(keys %$clusters)) {
                if($brief) {
                    print "$cn\n";
                }
                elsif($allusers) {
                    print
                        "$cn\t\t", $$clusters{$cn}->total,
                        "\t", $$clusters{$cn}->running,
                        "\t", $$clusters{$cn}->idle,
                        "\t", $$clusters{$cn}->held,
                        "\t", $u,
                        "\n";
                }
                else {
                    print
                        "$cn\t\t", $$clusters{$cn}->total,
                        "\t", $$clusters{$cn}->running,
                        "\t", $$clusters{$cn}->idle,
                        "\t", $$clusters{$cn}->held,
                        "\n";
                }
            }
        }
    }

}

exit 0;

#================================================================
