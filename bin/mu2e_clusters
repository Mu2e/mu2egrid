#!/usr/bin/perl -w
#
# A simple wrapper for jobsub_q to figure out what job clusters are still in the queue.
#
# A.Gaponenko, 2012, 2015
#
use strict;
use Getopt::Long;

#================================================================
sub usage() {
    return <<EOF
Usage:

        condor_clusters [--brief] [--user=username]

will display a list of all active condor clusters for the user and job
count in each cluster.  If the '--brief' options is given, just cluster
numbers are printed.

EOF
;
}

#================================================================
sub getUserClusters($) {
    my $username = shift;

    open OLDERR,     ">&", \*STDERR or die "Can't dup STDERR: $!";
    close STDERR;

    open(CQ, '-|', 'jobsub_q', '--user', $username)
        or die "Can not read jobsub_q output: $!\n";

    my %clusters;

    while(my $line = <CQ>) {
        chomp($line);
        my @fields = split(' ', $line);
#       print "got username = $username, fields = ". join('#', @fields) . "\n";
        if(($#fields > 5) && ($fields[1] eq $username)) {
            my $cn = $fields[0];
            $cn =~ s/\..*$//;
            my $cserver = $fields[0];
            $cserver =~ s/^.*@//;

            if(!defined($clusters{$cn})) {
                $clusters{$cn} = [0,0, $cserver];
            }

            # total job count
            ++$clusters{$cn}[0];
            # running jobs count
            if($fields[5] eq 'R') {
                ++$clusters{$cn}[1];
            }
        }
    }

    close CQ;

    open STDERR,     ">&", \*OLDERR or die "Can't restor STDERR: $!";

    return \%clusters;
}

#================================================================
my $user = `whoami`;
chomp $user;
my %opt = (help => 0, brief => 0, user => \$user );
GetOptions(\%opt, "help", "brief", "user=s") or die "\nError processing command line options.\n";

if($opt{'help'} or ($#ARGV >= 0)) {
    print usage();
}
else {
    my $brief = $opt{'brief'};

    my $clusters = getUserClusters($user);

    if(!$brief) {
        print "Cluster  \t\t\t\tnjobs\trunning\n";
    }

    foreach my $cn (sort(keys %$clusters)) {
        if($brief) {
            print "$cn\n";
        }
        else {
            print "$cn.0\@$$clusters{$cn}[2]\t\t$$clusters{$cn}[0]\t$$clusters{$cn}[1]\n";
        }
    }
}

exit 0;

#================================================================
